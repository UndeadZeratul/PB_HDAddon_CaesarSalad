// ------------------------------------------------------------
// Former Human
// ------------------------------------------------------------

class HDLiberatorZombie:HDHumanoid{
	default{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Zombieman"
		//$Sprite "POSSA1"

		radius 14;
		height 54;
		painchance 190;
		monster;
		+floorclip
		seesound "libguy/sight";
		painsound "libguy/pain";
		deathsound "libguy/death";
		activesound "libguy/active";
		tag "Shock Trooper";

		health 110;
		speed 10;
		mass 199;
		maxtargetrange 4000;
		obituary "%o got liberated from life by a zombie shock trooper.";
		hitobituary "%o battled with a zombie shock trooper.";
		hdmobbase.downedframe 11;
	}
	double turnamount;
	int user_weapon;
	int mag;
	int firemode; // -1 semi only; 0 semi; 1 auto;

	override void postbeginplay(){
		super.postbeginplay();
		
		bhashelmet;
		givearmour(1.,0.06,-0.4);
		
		if(user_weapon)accuracy=user_weapon;
		if(accuracy==1)firemode=random(0,3);
		else if(accuracy==2)firemode=-1;
		else firemode=random(-1,3);
		if(firemode==-1){
			mag=random(1,30);
		}else{
			mag=random(1,30);
			if(firemode>2)firemode=1;
			maxtargetrange=2500;
		}
	}

	void A_ZomFrag(){
		bool garbage;actor gg;
		double cpp=cos(pitch);double spp=sin(pitch);
		[garbage,gg]=A_SpawnItemEx("HDFragSpoon",
			cpp*-4,-3,height-6-spp*-4,
			cpp*3,0,-spp*3,
			frandom(30,45),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
		);
		gg.vel+=self.vel;
		double gforce=frandom(10,30);
		[garbage,gg]=A_SpawnItemEx("HDFragGrenade",
			0,0,height-6,
			cpp*gforce,0,-spp*gforce,
			0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
		);
		gg.vel+=self.vel;
	}
	
	void A_EjectLiberatorCasing(){
		HDWeapon.EjectCasing(self,"HDSpent7mm",
			-frandom(79,81),
			(frandom(7,7.5),0,0),
			(13,0,0)
		);
	}
	
	override void deathdrop(){
		if(bhasdropped&&bfriendly)return;
		hdweapon wp=null;{
			if(!bhasdropped){
				if(wp=DropNewWeapon("LiberatorRifle")){
					wp.weaponstatus[0]=LIBF_NOLAUNCHER|(random(0,1)*LIBS_CHAMBER);
					wp.weaponstatus[LIBS_MAG]=mag;
					if(firemode==-1)wp.weaponstatus[0]|=LIBF_NOAUTO;
				}
				DropNewItem("HDFragGrenadeAmmo",42);
			}else{
				DropNewItem("HD7mMag",96);
			}
		}
		if(!bhasdropped){
			DropNewItem("HDHandgunRandomDrop");
			bhasdropped=true;
		}
	}
	states{
	spawn:
		ZCM0 A 0;
	idle:
	spawn2:
		#### A 0{
			A_HDLook();
			A_Recoil(frandom(-0.1,0.1));
		}
		#### EEE 1{
			A_SetTics(random(5,17));
			A_HDLook();
		}
		#### E 1{
			A_Recoil(frandom(-0.1,0.1));
			A_SetTics(random(10,40));
		}
		#### B 0 A_Jump(28,"spawngrunt");
		#### B 0 A_Jump(132,"spawnswitch");
		#### B 8 A_Recoil(frandom(-0.2,0.2));
		loop;
	spawngrunt:
		#### G 1{
			A_Recoil(frandom(-0.4,0.4));
			A_SetTics(random(30,80));
			if(!random(0,7))A_Vocalize(activesound);
		}
		#### A 0 A_Jump(256,"spawn2");
	spawnswitch:
		#### A 0 A_JumpIf(bambush,"spawnstill");
		goto spawnwander;
	spawnstill:
		#### A 0 A_HDLook();
		#### A 0 A_Recoil(random(-1,1)*0.4);
		#### CD 5 A_SetAngle(angle+random(-4,4));
		#### A 0{
			A_HDLook();
			if(!random(0,127))A_Vocalize(activesound);
		}
		#### AB 5 A_SetAngle(angle+random(-4,4));
		#### B 1 A_SetTics(random(10,40));
		#### A 0 A_Jump(256,"spawn2");
	spawnwander:
		ZCM0 A 0 A_HDLook();
		#### CD 5 A_Wander();
		#### AB 5{
			A_HDLook();
			if(!random(0,15))A_Vocalize(activesound);
			A_Wander();
		}
		#### A 0 A_Jump(196,"spawn");
		loop;
	missile:
		#### A 0 A_JumpIf(mag<1,"reload");
	missile:
		#### ABCD 3 
		{
			A_TurnToAim(40,shootstate:"aiming");

			if (target) {
				double dt=distance3d(target);

				if(
					!random(0,5)
					&&dt>300
					&&dt<1000
				)setstatelabel("frag");
			}
		}
		loop;
	aiming:
		#### E 9 A_FaceLastTargetPos(30);
		#### E 1 A_StartAim(maxspread:20,maxtics:random(0,35));
		//fallthrough to shoot
	shoot:
		#### E 0 A_JumpIf(jammed,"jammed");
		#### E 0{
			pitch+=frandom(-spread,spread);
			angle+=frandom(-spread,spread);
		}
	fire:
		#### F 1 bright light("SHOT"){
			if(mag<1){
				setstatelabel("ohforfuckssake");
				return;
			}else{
				A_StartSound("weapons/bigrifle",CHAN_WEAPON);
				HDBulletActor.FireBullet(self,"HDB_776");
				A_EjectLiberatorCasing();
				pitch+=frandom(-spread,spread*0.5)*1.1;
				angle+=frandom(-spread*0.5,spread)*0.9;
				mag--;}
		}
		#### E 2{
			if(
				//conditions under which no autofire can happen
				!firemode
				||firemode==-1
				||firemode>3
				||mag<1
			){
				if(firemode>2)firemode=2;
				setstatelabel("postshot");
			}else spread++;
		}
		#### A 0 A_Jump(95,"shoot");
		//fallthrough to postshot
	postshot:
		#### E 12{
			if(!random(0,127))A_Vocalize(activesound);
			if(mag<1){
				setstatelabel("reload");
				return;
			}
			spread=max(0,spread-1);
			A_SetTics(random(2,6));
		}
		#### E 3;
		#### E 0 A_JumpIf(!hdmobai.tryshoot(self),"see");
		#### E 0 A_JumpIfTargetInLOS(1);
		goto coverfire;  //even if not in los,occasionally keep shooting anyway
		#### E 3 A_FaceTarget(10,10);
		#### E 0 A_Jump(50,"see");  //even if in los,occasionally stop shooting anyway
		goto coverfire;

	coverfire:
		#### E 1{
			spread=2;
			A_Coverfire();
			A_SetTics(random(2,6));
		}
		wait;

	frag:
		#### A 10 A_Vocalize(seesound);
		#### A 20{
			A_StartSound("weapons/pocket",CHAN_WEAPON);
			A_FaceTarget(0,0);
			pitch-=random(10,50);
		}
		#### E 15;
		#### E 10{
			A_SpawnItemEx("HDFragSpoon",cos(pitch)*4,-1,height-6-sin(pitch)*4,cos(pitch)*cos(angle)*4+vel.x,cos(pitch)*sin(angle)*4+vel.y,sin(-pitch)*4+vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			A_ZomFrag();
		}
		#### A 0 A_JumpIf(mag<1,"reload");
		#### A 0 setstatelabel("see");

	jammed:
		#### E 8;
		#### E 0 A_Jump(128,"see");
		#### E 4 A_Vocalize(random(0,2)?seesound:painsound);
		---- A 0 setstatelabel("see");

	ohforfuckssake:
		#### E 8;
	reload:
		---- A 0 A_JumpIf(mag<0,"unloadedreload");
		---- A 6 A_StartSound("weapons/rifleclick2");
		#### AA 1 A_HDChase("melee",null,flags:CHF_FLEE);
		#### A 0{
			A_StartSound("weapons/rifleunload");
			name emptymag="HD7mMag";
			HDMagAmmo.SpawnMag(self,emptymag,0);
			mag=-1;
		}
	unloadedreload:
		#### BCD 2 A_HDChase("melee",null,flags:CHF_FLEE);
		#### EE random(12,16) A_StartSound("weapons/pocket",8);
		#### E 12 {A_StartSound("weapons/rifleload",9);A_StartSound("weapons/rifleclick2");}
		#### E 8{
			A_StartSound("weapons/rifleclick2",8);
			mag=30;
		}
		#### E 4 {A_StartSound("weapons/libchamber2",8);}
		#### E 12 {A_StartSound("weapons/libchamber",8);}
		#### CCBB 2 A_HDWander();

	see:
		#### A 0{if(firemode>=0)firemode=random(0,2);}
	see2:
		#### A 0 A_JumpIf(!jammed&&mag<1,"reload");
		#### ABCD 4 A_HDChase();
		#### A 0{
			spread=2;
		}
		#### A 0 A_JumpIfTargetInLOS("see");
		#### A 0 A_Jump(24,"roam");
		loop;
	roam:
		#### E 3 A_Jump(60,"roam2");
		#### E 0{spread=1;}
		#### E 4 A_Watch();
		#### E 0{spread=0;}
		#### EEE 4 A_Watch();
		#### A 0 A_Jump(60,"roam");
	roam2:
		#### A 0 A_Jump(8,"see");
		#### ABCD 5 A_HDChase(speedmult:0.6);
		#### A 0 A_Jump(140,"Roam");
		#### A 0 A_JumpIfTargetInLOS("see");
		loop;
	turnaround:
		#### A 0 A_FaceTarget(15,0);
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### E 0{spread=3;}
		#### A 0 A_FaceTarget(15,0);
		#### E 0{spread=6;}
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### E 0{spread=4;}
		#### ABCD 3 A_HDChase();
		---- A 0 setstatelabel("see");
	pain:
		#### G 2;
		#### G 3 A_Vocalize(painsound);
		#### G 0{
			A_ShoutAlert(0.1,SAF_SILENT);
			if(target&&distance3d(target)<100)setstatelabel("see");
		}
		#### ABCD 2 A_HDChase(flags:CHF_FLEE);
		---- A 0 setstatelabel("see");


	falldown:
		#### H 5;
		#### I 5 A_Vocalize(deathsound);
		#### JJKKK 2 A_SetSize(-1,max(deathheight,height-10));
		#### L 0 A_SetSize(-1,deathheight);
		#### L 5;
		#### M 10 A_KnockedDown();
		wait;
	standup:
		#### LK 6;
		#### J 0 A_Jump(160,2);
		#### J 0 A_Vocalize(seesound);
		#### JI 4 A_Recoil(-0.3);
		#### HE 6;
		#### A 0 setstatelabel("see");

	death:
		ZCM0 H 5;
		ZCM0 I 5{
			A_Vocalize(deathsound);
		}
		ZCM0 J 5;
		ZCM0 KL 5;
	dead:
		ZCM0 L 1;
		ZCM0 M 5 canraise{
			if(abs(vel.z)>1)setstatelabel("dead");
		}wait;
	xxxdeath:
		ZCM0 O 3;
		ZCM0 P 3{
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
			A_XScream();
		}
		ZCM0 R 2;
		ZCM0 QRS 5;
		---- A 0 setstatelabel("xdead");

	xdeath:
		ZCM0 O 5;
		ZCM0 P 3{
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
			A_XScream();
		}
		ZCM0 R 2 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
		ZCM0 Q 5;
		ZCM0 Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
		ZCM0 RS 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
	xdead:
		ZCM0 S 3;
		ZCM0 T 5 canraise{
			if(abs(vel.z)>1)setstatelabel("dead");    
		}wait;
	raise:
		ZCM0 M 2 A_SpawnItemEx("MegaBloodSplatter",0,0,4,0,0,3,0,SXF_NOCHECKPOSITION);
		ZCM0 ML 6;
		ZCM0 KJIH 4;
		#### A 0 A_Jump(256,"see");
	ungib:
		ZCM0 T 6 A_SpawnItemEx("MegaBloodSplatter",0,0,4,0,0,3,0,SXF_NOCHECKPOSITION);
		ZCM0 TS 12 A_SpawnItemEx("MegaBloodSplatter",0,0,4,0,0,3,0,SXF_NOCHECKPOSITION);
		ZCM0 RQ 7;
		ZCM0 POH 5;
		#### A 0 A_Jump(256,"see");
	}
}

class HDAutoLibShockTrooper:HDLiberatorZombie{
	default{
		accuracy 1;
}}
class HDSemiLibShockTrooper:HDLiberatorZombie{
	default{
		accuracy 2;
}}

class HDZombieShockTrooperSpawn:RandomSpawner{
	default{
		dropitem "HDSemiLibShockTrooper",256,100;
		dropitem "HDAutoLibShockTrooper",256,50;
	}
}